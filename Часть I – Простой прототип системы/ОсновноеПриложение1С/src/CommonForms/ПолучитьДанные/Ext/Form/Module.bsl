
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПолучитьДанные(Команда)          
	
	ПолучитьДанныеНаСервереБезКонтекста();
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанныеФото = Base64Значение(Элемент.ТекущиеДанные.Данные);
	Фото = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФото, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ПолучитьДанныеНаСервереБезКонтекста()
	
	// Запрос данных с промежуточного сервера.
	Результат = КоннекторHTTP.GetJson("http://192.168.0.96:8000/get"); 
	
	Если ТипЗнч(Результат) = Тип("Соответствие") И Результат["status"] = "ok" Тогда
		
		// Массив данных хранится в поле "message".
		МассивДанных = Результат["message"];

		ДатаСобытия = ТекущаяДата();
		
		// Сохранение полученных данных.
		НаборЗаписей = РегистрыСведений.Фотографии.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ДатаСобытия.Установить(ДатаСобытия);
			
		Для каждого Данные Из МассивДанных Цикл
			
			ЗаписьНабора = НаборЗаписей.Добавить();
			ЗаписьНабора.ДатаСобытия	= ДатаСобытия;
			ЗаписьНабора.Идентификатор	= Данные["id"];
			ЗаписьНабора.Данные			= Данные["data"];
			
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти 


