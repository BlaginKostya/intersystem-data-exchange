				
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Фото(Команда)
	
	Если НЕ СредстваМультимедиа.ПоддерживаетсяФотоснимок(ТипКамерыУстройства.Задняя) Тогда
		ДобавитьСообщение("Устройство не поддерживает фотографирование.");
		Возврат;
	КонецЕсли;
	
	РазрешениеФото = Новый РазрешениеКамерыУстройства(640, 480);
	
	ДанныеМультимедиа = СредстваМультимедиа.СделатьФотоснимок(ТипКамерыУстройства.Задняя, РазрешениеФото);
	Если ДанныеМультимедиа = Неопределено Тогда
		ДобавитьСообщение("Не удалось сделать фото.");
		Возврат;     
	КонецЕсли;

	ДвоичныеДанныеФото = ДанныеМультимедиа.ПолучитьДвоичныеДанные();
	ТекущаяФотография = ПоместитьВоВременноеХранилище(ДвоичныеДанныеФото, УникальныйИдентификатор);
	
КонецПроцедуры 

&НаКлиенте
Процедура Отправить(Команда)
	
	// Проверка наличия фотографии.
	Если НЕ ЭтоАдресВременногоХранилища(ТекущаяФотография) Тогда
		ДобавитьСообщение("Сначала фото, после отправка.");
        Возврат;            
	КонецЕсли;
	
	// Создание нового HTTP соединения.
	Попытка
		Соединение = Новый HTTPСоединение("192.168.0.96", 8000);
	Исключение
		ДобавитьСообщение(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	              
	// Формирование заголовков.
	Заголовоки = Новый Соответствие();
	Заголовоки.Вставить("Content-Type", "application/json; charset=utf-8");
	
	// Создание HTTP запроса с заголовками.
	Запрос = Новый HTTPЗапрос("/add", Заголовоки);

	// Формирование структуры данных для отправки.
	Данные = Новый Структура;
	Данные.Вставить("id", Строка(Новый УникальныйИдентификатор));
                                                      
    ДвоичныеДанныеФото = ПолучитьИзВременногоХранилища(ТекущаяФотография);  
	
	Данные.Вставить("data", Base64Строка(ДвоичныеДанныеФото)); 
	
	// Сериализация данных в JSON.
	ЗаписьJSON = Новый ЗаписьJSON; 
	ЗаписьJSON.УстановитьСтроку(); 
	
	ЗаписатьJSON(ЗаписьJSON, Данные);
	
	Данные = ЗаписьJSON.Закрыть();
	
	// Установка сериализованных данных в тело запроса.
	Запрос.УстановитьТелоИзСтроки(Данные, КодировкаТекста.UTF8);
	       
	// Отправка данных методом POST.
	Попытка
		Ответ = Соединение.ВызватьHTTPМетод("POST", Запрос);
	Исключение  
		ДобавитьСообщение(ОписаниеОшибки());
		Возврат;
	КонецПопытки; 
	
	// Десериализация полученного ответа.
	Попытка     
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Ответ.ПолучитьТелоКакСтроку());
		
		Ответ = ПрочитатьJSON(ЧтениеJSON, Истина);
		
	Исключение
		ДобавитьСообщение(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
      
	ДобавитьСообщение(Ответ["status"] + " : " + Ответ["message"]);
	
КонецПроцедуры

#КонецОбласти     

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ДобавитьСообщение(Текст)
	
	ЖурналСообщений.Добавить(Текст + Символы.ПС);

КонецПроцедуры

#КонецОбласти

